#!/usr/bin/perl

use strict;
use warnings;

die "Usage: $0 index >clean-html\n"
   unless @ARGV==1;
my ($index) = @ARGV;

my $home     = 'http://perl.overmeer.net/mailbox';

sub manual2page($)
{   my $page = shift;
    $page  =~ s#\:\:#/#g;
    $page .= '.html';
    $page;
}

sub chapter($)
{    my $thing = substr $_[0], 0, 1;
     $thing =~ m/[a-zA-Z]/ ? uc $thing : '0';
}

sub link2manual($)
{    (my $manual = shift) =~ s/\.html.*//;
     $manual =~ s#/#::#g;
     $manual;
}

my %index;
sub read_index($)
{   my $filename = shift;

    local $_;

    open INDEX, '<', $filename
        or die "Cannot open index $filename: $!\n";

    my ($manual, $pkg);

    while(<INDEX>)
    {   next if m!^\s*$!;

        if( m!^([\w:-]+)\s+([\w:-]+)\s*$! )
        {   ($manual, $pkg) = ($1, $2);
            push @{$index{chapter $manual}{$manual}}, manual2page $manual;
        }
        elsif( m!^(\w+)\(\S*\)\s+([\w:-]+)\s*$! ) { ; }
        elsif( m!^(\w+)\(\S+\)\s*$! )             { ; }
        elsif( m!^(\w+)\(\)\s*$! )
        {   my $method = $1;
            push @{$index{chapter $method}{$method}},
                manual2page($manual)."#$method";
        }
        else
        {   warn "Illegal line $filename $.: $_";
        }
    }

    close INDEX;
}

read_index $index;

print <<HEAD;
<html>
<head><title>Mail::Box index</title>
<link rel=stylesheet href=pod.css type=text/css>
</head>
<body bgcolor=white>

HEAD

my $chapter_list = join '&nbsp;&nbsp;',
   map { "<a href=#$_>$_</a>" }  sort keys %index;
   
print <<INDEX;
<table width=100%>
  <tr><td align=center><h1>Mail::Box<h1></td></tr>
  <tr><td align=center><a href=$home>$home</a></td></tr>
  <tr><td align=center>$chapter_list</td></tr>
</table>
INDEX

foreach my $chapter (sort keys %index)
{   print "<h2><a name=$chapter>$chapter</a></h2>\n\n<table>";
    foreach my $entry (sort keys %{$index{$chapter}})
    {    my @links = map { "<a href=$_>".link2manual($_)."</a>" }
                         @{$index{$chapter}{$entry}};

         local $" = ",\n  ";
         print "<tr><td valign=top>$entry</td><td>\n  @links\n</td></tr>";
    }
    print "</table>\n";
}

print <<TAIL;
</body>
</html>
TAIL
